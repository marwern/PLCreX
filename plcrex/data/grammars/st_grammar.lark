// ********************************************************************************
// POU Declaration List
// ********************************************************************************
?start: type pou (idcl | vdcl )* statlist? end_type -> module
pou: NAME -> name

type: TYPE
TYPE: "PROGRAM" | "FUNCTION_BLOCK"

end_type: END_TYPE
END_TYPE: "END_PROGRAM" | "END_FUNCTION_BLOCK"


// ********************************************************************************
// Interface/Variable Declaration List
// ********************************************************************************
idcl: (var_input | var_output | var_in_out)* -> idcl

vdcl: var_local -> vdcl

dcllist: declaration*

declaration:
    | variable ":" datatype ";"
    | variable ":" datatype ":=" _expr ";"
    | variable ":" array ";"

var_input: "VAR_INPUT" dcllist "END_VAR"

var_output: "VAR_OUTPUT" dcllist "END_VAR"

var_in_out: "VAR_IN_OUT" dcllist "END_VAR"

var_local: "VAR" dcllist "END_VAR"


// ********************************************************************************
// Datatypes
// ********************************************************************************
datatype: TYPE_BOOL | TYPE_INT | TYPE_REAL | TYPE_TIME | macro_name

TYPE_BOOL: "BOOL"
TYPE_INT: "INT"
TYPE_REAL: "REAL"
TYPE_TIME: "TIME"
array: "ARRAY" "[1.." constant_int "] OF" datatype
macro_name: MACRO_NAME


// ********************************************************************************
// Functions/User-POUs
// ********************************************************************************
macro:
    | MACRO_NAME "." macro_out
    | MACRO_NAME "(" macro_assignment ("," macro_assignment)* ");"
    | MACRO_NAME "(" _expr ("," _expr )* ");"
macro_out: MACRO_NAME


// ********************************************************************************
// Arithmetic operators
// ********************************************************************************
multiply_with.1: _expr "*" _expr | "MUL(" _expr "," _expr ")"
divide_by.1: _expr "/" _expr | "DIV(" _expr "," _expr ")"
modulo.1: _expr "MOD" _expr | "MOD(" _expr "," _expr ")"
adding.1: _expr "+" _expr | "ADD(" _expr "," _expr ")"
subtracting.1: _expr "-" _expr | "SUB(" _expr "," _expr ")"
expt.1: "EXPT(" _expr "," _expr ")"
unaryminus.1: "-" _expr


// ********************************************************************************
// Boolean operators
// ********************************************************************************
not_op: "NOT" _expr
and_op: _expr "AND" _expr | "AND(" _expr "," _expr ")"
xor_op: _expr "XOR" _expr | "XOR(" _expr "," _expr ")"
or_op: _expr "OR" _expr | "OR(" _expr "," _expr ")"


// ********************************************************************************
// Numeric operators
// ********************************************************************************
less_or_equal.3: _expr "<=" _expr | "LE(" _expr "," _expr ")"
greater_or_equal.3: _expr ">=" _expr | "GE(" _expr "," _expr ")"
less_than.1: _expr "<" _expr | "LT(" _expr "," _expr ")"
greater_than.1: _expr ">" _expr | "GT(" _expr "," _expr ")"


// ********************************************************************************
// Equality
// ********************************************************************************
equality: _expr "=" _expr | "EQ(" _expr "," _expr ")"
inequality.2: _expr "<>" _expr | "NE(" _expr "," _expr ")"

// ********************************************************************************
// General expressions
// ********************************************************************************
_expr:
    | equality
	| variable ("[" constant_int "]")*
	| multiply_with
	| divide_by
	| modulo
	| adding
	| subtracting
	| "(" _expr ")"
    | expt
	| constant
	| constant_time
	| not_op
	| and_op
	| xor_op
	| or_op
	| inequality
	| less_or_equal
	| greater_or_equal
	| less_than
	| greater_than
	| false
	| true
    | unaryminus
    | if_statement_short

false: FALSE
FALSE: "FALSE"
true: TRUE
TRUE: "TRUE"


// ********************************************************************************
// Statementlist
// ********************************************************************************
constant_time.2: "t#" MS_DURATION "s"
constant.1: UINT | REAL
constant_int.1: UINT
UINT: /[0-9]+/
REAL: /[0-9][.]?[0-9]*/
MS_DURATION: UINT
//NUMBER: /[+-]?[0-9]+/

statlist: statement*

statement:
    | immediate_assignment
    | if_statement
    | while_statement
    | repeat_statement
    | macro
    | macro_assignment


// ********************************************************************************
// Assignments
// ********************************************************************************
immediate_assignment.1: variable("["constant_int"]")* ":=" _expr ";"

macro_assignment: variable ":=" _expr


// ********************************************************************************
// selection statement
// ********************************************************************************
if_statement: "IF" _expr "THEN" statlist "ELSE" statlist "END_IF;"

if_statement_short: "SEL(" _expr "," _expr "," _expr ")"



// ********************************************************************************
// iteration statements
// ********************************************************************************
while_statement: "WHILE" _expr "DO" statlist "END_WHILE;"

repeat_statement: "REPEAT" statlist "UNTIL" _expr "END_REPEAT;"


// ********************************************************************************
// Additional Terminals and_op rules
// ********************************************************************************
variable: NAME


// ********************************************************************************
// Lark statements
// ********************************************************************************
MULTI_LINE_COMMENT: /\(\*.*?\*\)/s
SINGLE_LINE_COMMENT: /\s*/ "//" /[^\n]/*

%ignore WS
%ignore MULTI_LINE_COMMENT
%ignore SINGLE_LINE_COMMENT
%import common.WS
NAME: /[_]?(?!(TRUE|FALSE|BOOL|INT|REAL|ARRAY\b))[a-zA-Z0-9_]\w*/
MACRO_NAME: /[_]?(?!(BOOL|INT|REAL|ARRAY\b))[a-zA-Z_]\w*/